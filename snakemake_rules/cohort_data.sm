# -----------------------------------------------------------------------------
# Target rule for complete cohort study
# -----------------------------------------------------------------------------
rule all:
        input:
                DCOHORT_VAL + "stat_overview_meqtl.pdf",
                DRANGES + "overview_meqtl.pdf",
                DCOHORT_VAL + "stat_overview_eqtlgen.pdf",
                DRANGES + "overview_eqtlgen.pdf"

#------------------------------------------------------------------------------
# Mediation analysis. This part can be performed indepdent of the graph fits
#------------------------------------------------------------------------------
rule mediation_analysis:
	input:
		data=DCOHORT_DATA + "{cohort}/{sentinel}_{seed}.rds",
		ranges=DRANGES + "{sentinel}_{seed}.rds"
	output:
		mediation=DMEDIATION + "{cohort}/{sentinel}_{seed}.rds",
		betas_per_gene=DMEDIATION + "{cohort}/{sentinel}_{seed}_betas_per_gene.pdf",
		beta_table=DMEDIATION + "{cohort}/{sentinel}_{seed}_betas.txt",
	threads: 1
	resources:
		mem_mb=600
	log:
		"logs/mediation_analysis/{cohort}/{sentinel}_{seed}.log"
	benchmark:
		"benchmarks/mediation_analysis/{cohort}/{sentinel}_{seed}.bmk"
	script:
		"../scripts/mediation_analysis.R"

#------------------------------------------------------------------------------
# Apply ggm on collected data and priors for a sentinel
#------------------------------------------------------------------------------
rule apply_ggm:
        input:
                data=DCOHORT_DATA + "{cohort}/{sentinel}_{seed}.rds",
                priors=DPRIORS + "{sentinel}_{seed}.rds",
                ranges=DRANGES + "{sentinel}_{seed}.rds",
                cpg_context="results/current/cpg_context.rds",
                tss_context="results/current/tfbs_tss_annot.rds",
                ppi_db=PPI_DB
        output:
                fit=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}.rds",
                summary_file=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}.pdf",
                gstart_file=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}-gstart.pdf"
        threads: 8
        resources:
                mem_mb=1000
        benchmark:
                "benchmarks/apply_ggm/{sentinel}_{cohort}_{seed}.bmk"
        log:
                "logs/apply_ggm/{sentinel}_{cohort}_{seed}.log"
        script:
                "../scripts/apply_ggm.R"

#------------------------------------------------------------------------------
# Validate calculated ggms
#------------------------------------------------------------------------------
# define graph types
GTYPES = ["bdgraph", "bdgraph_no_priors", "genenet", "irafnet"]
GTYPES_CONSTR = "|".join(GTYPES)
rule validate_ggm:
        input:
                ranges=DRANGES + "{sentinel}_{seed}.rds",
                gtex="data/current/geuvadis/GD660.GeneQuantRPKM.tsv",
                geo="data/current/archs4/whole_blood/expression_matrix_norm_peer.tsv",
                cis_kora="data/current/kora/eqtl/cis-full.tsv",
                trans_kora="data/current/kora/eqtl/trans-full.tsv",
                cis_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_cis-only_logFDR2.txt.gz",
                trans_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_trans-only_logFDR10.txt.gz",
                bonder_eqtm="data/current/bonder-et-al-2017/2015_09_02_cis_eQTMsFDR0.05-CpGLevel.txt",
                kora_data=DCOHORT_DATA + "kora/{sentinel}_{seed}.rds",
                lolipop_data=DCOHORT_DATA + "lolipop/{sentinel}_{seed}.rds",
                kora_fit=DCOHORT_FITS + "kora/{sentinel}_{seed}.rds",
                lolipop_fit=DCOHORT_FITS + "lolipop/{sentinel}_{seed}.rds",
		mediation_kora=DMEDIATION + "kora/{sentinel}_{seed}.rds",
		mediation_lolipop=DMEDIATION + "lolipop/{sentinel}_{seed}.rds"
        params:
                mediation_cutoff=0.05
        output:
                DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_{seed}.txt",
		mediation_summary_plot=DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_{seed}_mediation.pdf",
		graphdot=DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_{seed}_graphplot.dot",
		graphpdf=DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_{seed}_graphplot.pdf"
        wildcard_constraints:
                graph_type = GTYPES_CONSTR
        log:
                "logs/validate_ggm/{cohort}/{sentinel}_{graph_type}_{seed}.log"
        threads: 1
        benchmark:
                "benchmarks/validate_ggm/{cohort}/{sentinel}_{graph_type}_{seed}.bmk"
        resources:
                mem_mb=7000
        script:
                "../scripts/validate-ggm.R"

#------------------------------------------------------------------------------
# Collect validation information in single file for different seeds
#------------------------------------------------------------------------------
rule summarize_validation_meqtl:
        input: expand(DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_meqtl.txt", sentinel=MEQTL.sentinel, graph_type=GTYPES, cohort=COHORTS)
        output: DCOHORT_VAL + "validation_all_meqtl.txt"
        shell:
                "cat {input} | sort -r | uniq > {output}"

rule summarize_validation_eqtlgen:
        input: expand(DCOHORT_VAL + "{cohort}/{sentinel}_{graph_type}_eqtlgen.txt", sentinel=EQTLGEN.sentinel, graph_type=GTYPES, cohort=COHORTS)
        output: DCOHORT_VAL + "validation_all_eqtlgen.txt"
        shell:
                "cat {input} | sort -r | uniq > {output}"

#------------------------------------------------------------------------------
# Create summary report
#------------------------------------------------------------------------------
rule render_validation:
        input:
                DCOHORT_VAL + "validation_all_{seed}.txt"
        output:
                stats=DCOHORT_VAL + "stat_overview_{seed}.pdf",
                cratios=DCOHORT_VAL + "cluster_ratios_{seed}.pdf",
                expr=DCOHORT_VAL + "gene_expression_{seed}.pdf",
                gene_types=DCOHORT_VAL + "gene_types_{seed}.pdf",
                mediation=DCOHORT_VAL + "mediation_{seed}.pdf",
                mediation_perc=DCOHORT_VAL + "mediation_percentages_{seed}.pdf",
                mediation_distr=DCOHORT_VAL + "mediation_distributions_{seed}.pdf",
                perf=DCOHORT_VAL + "performance_{seed}.pdf"
        log:
                "logs/validation_{seed}.log"
        script:
                "../scripts/render-validation.R"

#------------------------------------------------------------------------------
# Generate dot files for visualization with cytoscape
#------------------------------------------------------------------------------
rule generate_dot:
        input:
                DCOHORT_FITS + "{cohort}/{sentinel}.rds"
        output:
                DCOHORT_FITS + "{cohort}/{sentinel}_{graph_type}.dot"
        log:
                "logs/generate_dot/{sentinel}_{graph_type}_{cohort}.log"
        script:
                "../scripts/generate_dot.R"


