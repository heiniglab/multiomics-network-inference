localrules: all_simulate_data, all_apply_ggm_simulation, all_simulation, all_simulation2, all_simulation3

#------------------------------------------------------------------------------
# Simulate ground truth and data for simulation study
#------------------------------------------------------------------------------
RUNS = 100
ITERATIONS = range(1,RUNS+1)

rule simulate_data:
        input:
                data=DCOHORT_DATA + "lolipop/{sentinel}_meqtl.rds",
                ranges=DRANGES + "{sentinel}_meqtl.rds",
                priors=DPRIORS + "{sentinel}_meqtl.rds"
        output:
                DSIM_DATA + "{sentinel}.RData"
        threads: 8
        resources:
                mem_mb=1500
        params:
                sentinel="{sentinel}",
                runs=RUNS,
                time="2-00:00:00"
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/simulate_data/{sentinel}.log"
        benchmark:
                "benchmarks/simulation/simulate_data/{sentinel}.bmk"
        script:
                "../scripts/simulation/simulate_data.R"

rule all_simulate_data:
	input:
		expand(DSIM_DATA + "{sentinel}.RData", sentinel=MEQTL.sentinel)

#------------------------------------------------------------------------------
# Apply ggm on simulated data
#------------------------------------------------------------------------------
rule apply_ggm_simulation:
        input:
                data=DSIM_DATA + "{sentinel}.RData",
                ppi_db=PPI_DB,
		cpg_context = "results/current/cpg_context.rds"
        output:
                DSIM_FITS + "{sentinel}-iter{iteration}.RData"
        params:
                iteration="{iteration}",
                time="2-00:00:00"
        threads: 12
        resources:
                mem_mb=2500
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/apply_ggm/{sentinel}-iter{iteration}.log"
        benchmark:
                "benchmarks/simulation/apply_ggm/{sentinel}-iter{iteration}.bmk"
        script:
                "../scripts/simulation/apply_ggm_simulation.R"

rule all_apply_ggm_simulation:
	input:
		expand(DSIM_FITS + "{sentinel}-iter{iteration}.RData", sentinel=MEQTL.sentinel,
		iteration=ITERATIONS)

rule apply_ggm_simulation_sentinels:
	input:
		expand(DSIM_FITS + "{sentinel}-iter1.RData", sentinel=MEQTL.sentinel)

rule apply_ggm_simulation_sentinels2:
	input:
		expand(DSIM_FITS + "{sentinel}-iter2.RData", sentinel=MEQTL.sentinel)

#------------------------------------------------------------------------------
# Validate a simulation run
#------------------------------------------------------------------------------
rule validate_ggm_simulation:
        input:
                fits=DSIM_FITS + "{sentinel}-iter{iteration}.RData",
        output:
                DSIM_VALIDATION + "{sentinel}-iter{iteration}.txt"
        threads: 1
        resources:
                mem_mb=400
        params:
                iteration="{iteration}",
                time="00:10:00"
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/validate_ggm/{sentinel}-iter{iteration}.log"
        script:
                "../scripts/simulation/validate_ggm_simulation.R"

#------------------------------------------------------------------------------
# Target rule to validate all simulation runs
#------------------------------------------------------------------------------
TEMP = glob_wildcards(DSIM_FITS + "{sentinel}-iter{iteration}.RData")
rule validate_subset:
	input:
		expand(DSIM_VALIDATION + "{sentinel}-iter{iter}.txt", zip, sentinel=TEMP.sentinel, iter=TEMP.iteration)

rule validate_all:
       input: expand(DSIM_VALIDATION + "{sentinel}-iter{iter}.txt", sentinel=MEQTL.sentinel, iter=ITERATIONS)


#------------------------------------------------------------------------------
# Get a summary file for all iterations and sentinels in the simulation study
#------------------------------------------------------------------------------
rule all_simulation:
	input:
		expand(DSIM_VALIDATION + "{sentinel}-iter{iter}.txt", sentinel=MEQTL.sentinel, iter=ITERATIONS)
	output:
		"results/current/biogrid/simulation/validation/validation.tsv"
	shell:
		"""
		cat {input} | sort -r | uniq > {output}
		"""
