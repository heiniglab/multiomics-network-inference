SENT_COHO = glob_wildcards("data/current/cohorts/{sentinel}.{cohort}.adjusted.data.RData")


localrules: all, preprocess, summarize_validation, simulate_data, all_sim, validate_ggm_simulation


#####
# Rules for application of GGM on real data
#####

rule preprocess:
	output:
		"results/current/data.processed.RData"
	log:
		"logs/preprocess.log"
	script:
		"R/preprocess.R"
		
rule apply_ggm:
	input: "results/current/data.processed.RData"
	output: "results/current/fits/{sentinel}.RData"
	threads: 10
	params:
		plotdir="results/current/plots/",
		nriter=20000,
		burnin=5000
	resources:
		mem_mb=1000
	benchmark:
		"benchmarks/apply-ggm/{sentinel}.bmk"
	log:
		"logs/apply-ggm/{sentinel}.log"
	script:
		"R/apply-ggm.R"

rule validate_ggm:
	input: rules.preprocess.output, "results/current/fits/{sentinel}.RData"
	output: "results/current/validation/{sentinel}.txt"
	log: "logs/validate-ggm/{sentinel}.log"
	benchmark:
		"benchmarks/validate-ggm/{sentinel}.bmk"
	resources:
		mem_mb=4000
	script:
		"R/validate-ggm.R"

rule summarize_validation:
	input: expand("results/current/validation/{sentinel}.txt", zip, sentinel=SENT_COHO.sentinel)
	output: txt="results/current/validation/validation.all.txt", pdf="results/current/validation.html"
	log:	"logs/validation.log"
	script:
		"R/render-validation.Rmd"

rule all:
	input: "results/current/validation.html"


#####
# Rules for simulation study
#####

SENT = glob_wildcards("data/current/networks/{sentinel}.RData")


rule simulate_data:
	input: "results/current/data.processed.RData"
	output: 
		expand("results/current/simulation/data/{sentinel}.RData", zip, sentinel=SENT.sentinel),
		odir="results/current/simulation/data/"
	params:
		random_walk_results="data/current/networks/"
	log:	"logs/simulation/simulate_data.log"
	benchmark: "benchmarks/simulation/simulate_data.bmk"
	script:
		"R/simulate-data.R"

rule apply_ggm_simulation:
	input: "results/current/simulation/data/{sentinel}.RData"
	output: "results/current/simulation/fits/{sentinel}.RData"
	params:
		plotdir="results/current/simulation/plots/",
		iter=20000,
		burnin=5000
	threads: 10
        resources:
                mem_mb=1000
	log: "logs/simulation/apply-ggm.{sentinel}.log"
	benchmark: "benchmarks/simulation/apply-ggm.{sentinel}.bmk"
	script:
		"R/apply-ggm-simulation.R"

rule validate_ggm_simulation:
	input: "results/current/simulation/fits/{sentinel}.RData"
	output: "results/current/simulation/validation/{sentinel}.txt"
	log: "logs/simulation/validate-ggm.{sentinel}.log"
	script:
		"R/validate-ggm-simulation.R"

rule summarize_simulation:
	input: expand("results/current/simulation/validation/{sentinel}.txt", zip, sentinel=SENT.sentinel)
	output: "R/summarize-simulation.html"
	log: "logs/simulation/summarize-simulation.log"
	script:
		"R/summarize-simulation.Rmd"

rule all_sim:
	input: "R/summarize-simulation.html"
