# -----------------------------------------------------------------------------
# Target rule for complete cohort study
# -----------------------------------------------------------------------------
rule all:
        input:
                DCOHORT_VAL + "stat_overview.pdf",
                DRANGES + "overview.pdf"

#------------------------------------------------------------------------------
# Apply ggm on collected data and priors for a sentinel
#------------------------------------------------------------------------------
rule apply_ggm:
        input:
                data=DCOHORT_DATA + "{cohort}/{sentinel}.rds",
                priors=DPRIORS + "{sentinel}.rds",
                ranges=DRANGES + "{sentinel}.rds",
                cpg_context="data/current/cpgs_with_chipseq_context_100.RData",
                string="results/current/string.v9.expr.rds"
        output:
                fit=DCOHORT_FITS + "{cohort}/{sentinel}.rds",
                summary_file=DCOHORT_FITS + "{cohort}/{sentinel}.pdf",
                gstart_file=DCOHORT_FITS + "{cohort}/{sentinel}-gstart.pdf"
        threads: 8
        params:
                nriter=20000,
                burnin=5000
        resources:
                mem_mb=1000
        benchmark:
                "benchmarks/apply_ggm/{sentinel}_{cohort}.bmk"
        log:
                "logs/apply_ggm/{sentinel}_{cohort}.log"
        script:
                "../scripts/apply-ggm.R"

#------------------------------------------------------------------------------
# Validate calculated ggms
#------------------------------------------------------------------------------
rule validate_ggm:
        input:
                ranges=DRANGES + "{sentinel}.rds",
                gtex="data/current/geuvadis/GD660.GeneQuantRPKM.tsv",
                geo="data/current/archs4/whole_blood/expression_matrix_norm_peer.tsv",
                cis_kora="data/current/kora/eqtl/cis-full.tsv",
                trans_kora="data/current/kora/eqtl/trans-full.tsv",
                cis_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_cis-only_logFDR2.txt.gz",
                trans_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_trans-only_logFDR10.txt.gz",
                bonder_eqtm="data/current/bonder-et-al-2017/2015_09_02_cis_eQTMsFDR0.05-CpGLevel.txt",
                kora_data=DCOHORT_DATA + "kora/{sentinel}.rds",
                lolipop_data=DCOHORT_DATA + "lolipop/{sentinel}.rds",
                kora_fit=DCOHORT_FITS + "kora/{sentinel}.rds",
                lolipop_fit=DCOHORT_FITS + "lolipop/{sentinel}.rds"
        params:
                dmediation_plots=DCOHORT_VAL + "mediation_plots/",
                mediation_cutoff=0.05
        output:
                DCOHORT_VAL + "{sentinel}.txt",
                mediation_detail=DCOHORT_VAL + "mediation_plots/{sentinel}_betas_by_gene.pdf",


        log:
                "logs/validate-ggm/{sentinel}.log"
        threads: 1
        benchmark:
                "benchmarks/validate-ggm/{sentinel}.bmk"
        resources:
                mem_mb=3000
        script:
                "../scripts/validate-ggm.R"

#------------------------------------------------------------------------------
# Collect validation information in single file
#------------------------------------------------------------------------------
rule summarize_validation:
        input: expand(DCOHORT_VAL + "{sentinel}.txt", zip, sentinel=LISTS.sentinel)
        output: DCOHORT_VAL + "validation.all.txt"
        shell:
                "cat {input} | sort -r | uniq > {output}"

#------------------------------------------------------------------------------
# Create summary report
#------------------------------------------------------------------------------
rule render_validation:
        input:
                DCOHORT_VAL + "validation.all.txt"
        output:
                stats=DCOHORT_VAL + "stat_overview.pdf",
                cratios=DCOHORT_VAL + "cluster_ratios.pdf",
                expr=DCOHORT_VAL + "gene_expression.pdf",
                gene_types=DCOHORT_VAL + "gene_types.pdf",
                mediation=DCOHORT_VAL + "mediation.pdf",
                mediation_perc=DCOHORT_VAL + "mediation_percentages.pdf",
                mediation_distr=DCOHORT_VAL + "mediation_distributions.pdf",
                perf=DCOHORT_VAL + "performance.pdf"
        log:
                "logs/validation.log"
        script:
                "../scripts/render-validation.R"

#------------------------------------------------------------------------------
# Generate dot files for visualization with cytoscape
#------------------------------------------------------------------------------
rule generate_dot:
        input:
                DCOHORT_FITS + "{cohort}/{sentinel}.rds"
        output:
                DCOHORT_FITS + "{cohort}/{sentinel}_{graph_type}.dot"
        log:
                "logs/generate_dot/{sentinel}_{graph_type}_{cohort}.log"
        script:
                "../scripts/generate_dot.R"


