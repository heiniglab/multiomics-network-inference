localrules: all_simulate_data, all_apply_ggm_simulation, all_simulation, all_simulation2, all_simulation3

#------------------------------------------------------------------------------
# Simulate ground truth and data for simulation study
#------------------------------------------------------------------------------
RUNS = 100
ITERATIONS = range(1,RUNS+1)
SUBSETS = [str(n) for n in range(50,650,50)]
#SUBSETS = [str(n) for n in range(10,210,10)]
#SUBSETS.append("all")

rule simulate_data:
        input:
                data=DCOHORT_DATA + "lolipop/{sentinel}_meqtl.rds",
                ranges=DRANGES + "{sentinel}_meqtl.rds",
                priors=DPRIORS + "{sentinel}_meqtl.rds"
        output:
                DSIM_DATA + "{sentinel}.RData"
        threads: 10
        resources:
                mem_mb=1500
        params:
                sentinel="{sentinel}",
                runs=RUNS,
                time="1-00:00:00"
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/simulate_data/{sentinel}.log"
        benchmark:
                "benchmarks/simulation/simulate_data/{sentinel}.bmk"
        script:
                "../scripts/simulation/simulate_data.R"

rule all_simulate_data:
	input:
		expand(DSIM_DATA + "{sentinel}.RData", sentinel=MEQTL.sentinel)

#------------------------------------------------------------------------------
# Apply ggm on simulated data
#------------------------------------------------------------------------------
rule apply_ggm_simulation:
        input:
                data=DSIM_DATA + "{sentinel}.RData",
                ppi_db=PPI_DB,
		cpg_context = "results/current/cpg_context.rds"
        output:
                DSIM_FITS + "{sentinel}-iter{iteration}-subset{subset}.RData"
        params:
                iteration="{iteration}",
                time="2-00:00:00"
        threads: 16
        wildcard_constraints:
                subset = "all|[0-9]+0*"
        resources:
                mem_mb=2000
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/apply_ggm/{sentinel}-iter{iteration}-subset{subset}.log"
        benchmark:
                "benchmarks/simulation/apply_ggm/{sentinel}-iter{iteration}-subset{subset}.bmk"
        script:
                "../scripts/simulation/run_ggm.R"

rule all_apply_ggm_simulation:
	input:
		expand(DSIM_FITS + "{sentinel}-iter{iteration}-subset{subset}.RData", sentinel=MEQTL.sentinel,
		iteration=ITERATIONS, subset=SUBSETS)

rule apply_ggm_simulation_subsets:
	input:
		expand(DSIM_FITS + "{sentinel}-iter{iter}-subset{subset}.RData", sentinel=MEQTL.sentinel, iter=ITERATIONS, subset=SUBSETS)

rule apply_ggm_simulation_sentinels:
	input:
		expand(DSIM_FITS + "{sentinel}-iter1.RData", sentinel=MEQTL.sentinel)

# for the rs60626639 sentinel, simulation seems to take too long at the moment, so we should recalculate
# on a specialiced run (e.g. more runtime or threads)
# This is only an issue for the "all" subset, as we do 11 inference cycles (for each noisey matrix).
MEQTL_SIM = MEQTL.sentinel
MEQTL_SIM.remove("rs60626639")

rule apply_ggm_simulation_iterations:
	input:
		expand(DSIM_FITS + "{sentinel}-iter{iter}-subsetall.RData", iter=ITERATIONS, sentinel=MEQTL_SIM)

#------------------------------------------------------------------------------
# Validate a simulation run for the 'all' subset
#------------------------------------------------------------------------------
rule validate_ggm_simulation:
        input:
                fits=expand(DSIM_FITS + "{{sentinel}}-iter{iteration}-subset{{subset}}.RData", iteration=ITERATIONS)
        output:
                DSIM_VALIDATION + "{sentinel}-subset{subset}.txt"
        threads: 1
        wildcard_constraints:
                subset = "all|[0-9]+0*"
        resources:
                mem_mb=400
        params:
                time="00:10:00"
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/simulation/validate_ggm/{sentinel}_subset{subset}.log"
        script:
                "../scripts/simulation/validate.R"

#------------------------------------------------------------------------------
# Target rule to validate all simulation runs
#------------------------------------------------------------------------------
rule validate_all:
	input: expand(DSIM_VALIDATION + "{sentinel}-subsetall.txt", sentinel=MEQTL.sentinel)
	output:
		DSIM + "validation-subsetall.txt"		
	resources:
		mem_mb = 1000
	threads: 1
	log: ""
	shell:
		"""
		sort -r {input} | uniq > {output}
		"""

rule validate_all_subsets:
	input: expand(DSIM_VALIDATION + "{sentinel}-subset{subset}.txt", sentinel=MEQTL.sentinel, subset=SUBSETS)
	output:
		DSIM + "validation-subsets.txt"		
	resources:
		mem_mb = 1000
	threads: 1
	log: ""
	shell:
		"""
		sort -r {input} | uniq > {output}
		"""

#------------------------------------------------------------------------------
# Get a summary file for all iterations and sentinels in the simulation study
#------------------------------------------------------------------------------
rule all_simulation:
	input:
		expand(DSIM_VALIDATION + "{sentinel}-iter{iter}.txt", sentinel=MEQTL.sentinel, iter=ITERATIONS)
	output:
		DSIM_VALIDATION + "validation.tsv"
	shell:
		"""
		cat {input} | sort -r | uniq > {output}
		"""

rule summarize_snp_gene_recovery:
	output:
		summary = DSIM_VALIDATION + "snp_gene_recovery/{sentinel}.tsv"
	params:
		dresults = DSIM_FITS,
		time = "01:00:00"
	log:
		"logs/simulation/snp_gene_recovery/{sentinel}.log"
	benchmark:
		"benchmarks/simulation/snp_gene_recovery/{sentinel}.bmk"
	threads: 6
	resources:
		mem_mb = 1000
	script:
		"../scripts/simulation/summarize_snp_gene_recovery.R"

rule all_summarize_snp_gene_recovery:
	input:
		expand(DSIM_VALIDATION + "snp_gene_recovery/{sentinel}.tsv", sentinel = MEQTL.sentinel)
	output:
		DSIM_VALIDATION + "snp_gene_recovery/all.tsv"
	shell:
		"""
		cat {input} | sort -r | uniq > {output}
		"""
