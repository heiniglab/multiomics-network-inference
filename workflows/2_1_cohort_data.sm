localrules: all_cohort, summarize_validation_eqtlgen, summarize_validation_meqtl,
            render_validation, all_ggm, all_summarize_validation,
            plot_relevant_graphs

#-------------------------------------------------------------------------------
# Target rule to run the complete cohort study.
# Note: this runs a long time and should definitely be executed on a cluster
#-------------------------------------------------------------------------------
rule all_cohort:
        input:
                DCOHORT_VAL + "validation_all_meqtl_gwas.txt",
                DCOHORT_VAL + "validation_all_eqtlgen_gwas.txt",
                DRANGES + "summary.pdf",
                DRANGES + "summary.pdf"

#------------------------------------------------------------------------------
# Mediation analysis. This part can be performed indepdent of the graph fits
#------------------------------------------------------------------------------
rule mediation_analysis:
	input:
		data=DCOHORT_DATA + "{cohort}/{sentinel}_{seed}.rds",
		ranges=DRANGES + "{sentinel}_{seed}.rds"
	output:
		mediation=DMEDIATION + "{cohort}/{sentinel}_{seed}.rds",
		betas_per_gene=DMEDIATION + "{cohort}/{sentinel}_{seed}_betas_per_gene.pdf",
		beta_table=DMEDIATION + "{cohort}/{sentinel}_{seed}_betas.txt",
	threads: 1
	resources:
		mem_mb=1000
	params:
		time="00:20:00"
	conda:
		"../envs/bioR.yaml"
	log:
		"logs/mediation_analysis/{cohort}/{sentinel}_{seed}.log"
	benchmark:
		"benchmarks/mediation_analysis/{cohort}/{sentinel}_{seed}.bmk"
	script:
		"../scripts/mediation.R"

rule all_mediation:
	input:
		expand(DMEDIATION + "kora/{sentinel}_eqtlgen.rds", sentinel=EQTLGEN.sentinel),
		expand(DMEDIATION + "kora/{sentinel}_meqtl.rds", sentinel=MEQTL.sentinel),
		expand(DMEDIATION + "lolipop/{sentinel}_eqtlgen.rds", sentinel=EQTLGEN.sentinel),
		expand(DMEDIATION + "lolipop/{sentinel}_meqtl.rds", sentinel=MEQTL.sentinel)

#------------------------------------------------------------------------------
# Apply ggm on collected data and priors for a sentinel
#------------------------------------------------------------------------------
rule apply_ggm:
        input:
                data=DCOHORT_DATA + "{cohort}/{sentinel}_{seed}.rds",
                priors=DPRIORS + "{sentinel}_{seed}.rds",
                ranges=DRANGES + "{sentinel}_{seed}.rds",
                cpg_context="results/current/cpg_context.rds",
                tss_context="results/current/tfbs_tss_annot.rds",
                ppi_db=PPI_DB
        output:
                fit=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}.rds",
                summary_file=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}.pdf"
#                gstart_file=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}_gstart.pdf"
        threads: 16
        params:
                time="2-00:00:00"
        resources:
                mem_mb=3000
        conda:
                "../envs/bioR.yaml"
        benchmark:
                "benchmarks/apply_ggm/{sentinel}_{cohort}_{seed}.bmk"
        log:
                "logs/apply_ggm/{sentinel}_{cohort}_{seed}.log"
        script:
                "../scripts/apply_ggm.R"

#------------------------------------------------------------------------------
# Meta target rule to generate all GGMs
#------------------------------------------------------------------------------
rule all_ggm:
	input:
		expand(DCOHORT_FITS + "lolipop/{sentinel}_meqtl.rds", sentinel=MEQTL.sentinel),
		expand(DCOHORT_FITS + "kora/{sentinel}_meqtl.rds", sentinel=MEQTL.sentinel),
		expand(DCOHORT_FITS + "kora/{sentinel}_eqtlgen.rds", sentinel=EQTLGEN.sentinel),
		expand(DCOHORT_FITS + "lolipop/{sentinel}_eqtlgen.rds", sentinel=EQTLGEN.sentinel)
		
#------------------------------------------------------------------------------
# Validate calculated ggms
#------------------------------------------------------------------------------
# define graph types
GTYPES = ["bdgraph", "bdgraph_no_priors", "genenet", "irafnet", "glasso", "glasso_no_priors", "genie3"]
GTYPES_CONSTR = "|".join(GTYPES)
rule validate_ggm:
        input:
                ranges=DRANGES + "{sentinel}_{seed}.rds",
                gtex="data/current/geuvadis/GD660.GeneQuantRPKM.tsv",
                geo="data/current/archs4/whole_blood/expression_matrix_norm_peer.tsv",
                cis_kora="data/current/kora/eqtl/cis-full.tsv",
                trans_kora="data/current/kora/eqtl/trans-full.tsv",
                cis_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_cis-only_logFDR2.txt",
                trans_joehanes="data/current/joehanes-2017/eqtls/eqtl-gene-annot_trans-only_logFDR10.txt",
                bonder_eqtm="data/current/bonder-et-al-2017/2015_09_02_cis_eQTMsFDR0.05-CpGLevel.txt",
                kora_data=DCOHORT_DATA + "kora/{sentinel}_{seed}.rds",
                lolipop_data=DCOHORT_DATA + "lolipop/{sentinel}_{seed}.rds",
                kora_fit=DCOHORT_FITS + "kora/{sentinel}_{seed}.rds",
                lolipop_fit=DCOHORT_FITS + "lolipop/{sentinel}_{seed}.rds",
#                kora_fit_old="results/old/biogrid/fits/kora/{sentinel}_{seed}.rds",
#                lolipop_fit_old="results/old/biogrid/fits/lolipop/{sentinel}_{seed}.rds",
		mediation_kora=DMEDIATION + "kora/{sentinel}_{seed}.rds",
		mediation_lolipop=DMEDIATION + "lolipop/{sentinel}_{seed}.rds"
        output:
                DCOHORT_VAL + "{cohort}/{sentinel}_{seed}.txt"
#		mediation_summary_plot=DCOHORT_VAL + "{cohort}/{sentinel}_{seed}_mediation.pdf"
        params:
                mediation_cutoff=0.05,
                time="7:00:00"
        threads: 7
        resources:
                mem_mb=2500
        conda:
                "../envs/bioR.yaml"
        log:
                "logs/validate_ggm/{cohort}/{sentinel}_{seed}.log"
        benchmark:
                "benchmarks/validate_ggm/{cohort}/{sentinel}_{seed}.bmk"
        script:
                "../scripts/validation.R"

#------------------------------------------------------------------------------
# Collect validation information in single file for different seeds
#------------------------------------------------------------------------------
rule summarize_validation_meqtl:
        input: 
                kora=expand(DCOHORT_VAL + "kora/{sentinel}_meqtl.txt", sentinel=MEQTL.sentinel),
                lolipop=expand(DCOHORT_VAL + "lolipop/{sentinel}_meqtl.txt", sentinel=MEQTL.sentinel)
        output: 
                kora=temp(DCOHORT_VAL + "meqtl_kora.tmp"),
                lolipop=temp(DCOHORT_VAL + "meqtl_lolipop.tmp"),
                all=DCOHORT_VAL + "validation_all_meqtl.txt"
        shell:
                """
		cat {input.kora} | sort -r | uniq > {output.kora}
		cat {input.lolipop} | sort -r | uniq > {output.lolipop}
                cat {output.kora} {output.lolipop} | sort -r | uniq > {output.all}
                """

#------------------------------------------------------------------------------
# Get all validation results for eQTLgen in a single file.
#
# NOTE: going via the params/pattern approach is necessary. Otherwise
# the 'argument list' for cat is too long and we get an error
#------------------------------------------------------------------------------
rule summarize_validation_eqtlgen:
        input: 
                kora=expand(DCOHORT_VAL + "kora/{sentinel}_eqtlgen.txt", sentinel=EQTLGEN.sentinel),
                lolipop=expand(DCOHORT_VAL + "lolipop/{sentinel}_eqtlgen.txt", sentinel=EQTLGEN.sentinel)
        output: 
                kora=temp(DCOHORT_VAL + "eqtlgen_kora.tmp"),
                lolipop=temp(DCOHORT_VAL + "eqtlgen_lolipop.tmp"),
                all=DCOHORT_VAL + "validation_all_eqtlgen.txt"
        params:
                kora_files = DCOHORT_VAL + "kora/*_eqtlgen.txt",
                lolipop_files = DCOHORT_VAL + "lolipop/*_eqtlgen.txt"
        shell:
                """
		cat {params.kora_files} | sort -r | uniq > {output.kora}
		cat {params.lolipop_files} | sort -r | uniq > {output.lolipop}
                cat {output.kora} {output.lolipop} | sort -r | uniq > {output.all}
		"""

#------------------------------------------------------------------------------
# Summarize validation over all loci, adding GWAS information, too
#------------------------------------------------------------------------------
rule all_summarize_validation:
	input:
		DCOHORT_VAL + "validation_all_eqtlgen.txt",
		DCOHORT_VAL + "validation_all_meqtl.txt"

#------------------------------------------------------------------------------
# Add GWAS information to validation table
#------------------------------------------------------------------------------
rule add_gwas_information:
	input:
		validation=DCOHORT_VAL + "validation_all_{seed}.txt",
		gwas="data/current/gwas/gwas_catalog_v1.0.2-associations_e96_r2019-07-12.tsv",
		eqtlgen="data/current/eqtl_gen/cis-eQTL_significant_20181017.txt.gz"
	output:
		validation=DCOHORT_VAL + "validation_all_{seed}_gwas.txt"
	params:
		time = "10:00:00"
	resources:
		mem_mb=5000
	threads: 6
	log:
		"logs/add_gwas_information_{seed}.log"
	script:
		"../scripts/validation_annotate_gwas.R"

#------------------------------------------------------------------------------
# Create summary report
#------------------------------------------------------------------------------
rule render_validation:
        input:
                DCOHORT_VAL + "validation_all_{seed}.txt"
        output:
                stats=DCOHORT_VAL + "stat_overview_{seed}.pdf",
                cratios=DCOHORT_VAL + "cluster_ratios_{seed}.pdf",
                expr=DCOHORT_VAL + "gene_expression_{seed}.pdf",
                gene_types=DCOHORT_VAL + "gene_types_{seed}.pdf",
                mediation=DCOHORT_VAL + "mediation_{seed}.pdf",
                mediation_perc=DCOHORT_VAL + "mediation_percentages_{seed}.pdf",
                mediation_distr=DCOHORT_VAL + "mediation_distributions_{seed}.pdf",
                perf=DCOHORT_VAL + "performance_{seed}.pdf"
        log:
                "logs/validation_{seed}.log"
        script:
                "../scripts/render-validation.R"

#------------------------------------------------------------------------------
# Generate dot files for visualization with cytoscape
#------------------------------------------------------------------------------
rule generate_dot:
	input:
		fits=DCOHORT_FITS + "{cohort}/{sentinel}_{seed}.rds",
	output:
		DGRAPH_PLOTS + "{sentinel}_{seed}/{cohort}/{graph_type}.dot"
	params:
		time = "00:30:00"
	resources:
		mem_mb=200
	threads: 1
 	log:
		"logs/generate_dot/{sentinel}_{seed}/{cohort}_{graph_type}.log"
	script:
		"../scripts/generate_dot.R"

#------------------------------------------------------------------------------
# Generate dot files for visualization with cytoscape, combine graphs from 
# both cohorts.
#------------------------------------------------------------------------------
rule generate_dot_combined:
	input:
		new_kora=DCOHORT_FITS + "kora/{sentinel}_{seed}.rds",
		new_lolipop=DCOHORT_FITS + "lolipop/{sentinel}_{seed}.rds",
		ranges = DRANGES + "{sentinel}_{seed}.rds",
		ppi_db = PPI_DB,
		cpg_context="results/current/cpg_context.rds",
		tss_context="results/current/tfbs_tss_annot.rds",
	params:
		time = "00:30:00"
	resources:
		mem_mb=200
	threads: 1
	output:
		dot = DGRAPH_PLOTS + "{sentinel}_{seed}/{graph_type}_combined.dot",
		graph = DGRAPH_PLOTS + "{sentinel}_{seed}/{graph_type}_combined.rds"
	log:
		"logs/generate_dot_combined/{sentinel}_{seed}/{graph_type}.log"
	script:
		"../scripts/generate_dot_combined.R"

#------------------------------------------------------------------------------
# Generate PDF containing cllection of all plotted graphs for an individual
# locus
#------------------------------------------------------------------------------
rule plot_graphs:
	input:
		kora=expand(DGRAPH_PLOTS + "{{sentinel}}_{{seed}}/kora/{graph_type}.dot", graph_type = GTYPES),
		lolipop=expand(DGRAPH_PLOTS + "{{sentinel}}_{{seed}}/lolipop/{graph_type}.dot", graph_type = GTYPES),
		combined=expand(DGRAPH_PLOTS + "{{sentinel}}_{{seed}}/{graph_type}_combined.dot", graph_type = GTYPES)
	output:
		pdf=DGRAPH_PLOTS + "{sentinel}_{seed}/all.pdf"
	params:
		odir=DGRAPH_PLOTS + "{sentinel}_{seed}/",
		time = "00:30:00"
	resources:
		mem_mb=200
	threads: 1
	log:
		"logs/plot_graphs/{sentinel}_{seed}.log"
	shell:
		"""
		cd {params.odir}

		for i in kora/*.dot ; do twopi -Tpdf $i > $(basename $i .dot)_kora.pdf ; done 
		for i in lolipop/*.dot ; do twopi -Tpdf $i > $(basename $i .dot)_lolipop.pdf ; done 
		for i in {input.combined} ; do twopi -Tpdf $(basename $i) > $(basename $i .dot).pdf ; done 

		gs -dBATCH -dNOPAUSE -q -sDEVICE=pdfwrite -sOutputFile=all.pdf *.pdf
		"""

#------------------------------------------------------------------------------
# Generate summary graph PDFs for few of the more interesting loci
#------------------------------------------------------------------------------
rule plot_relevant_graphs:
	input:
		DGRAPH_PLOTS + "rs8049417_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs13256023_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs1364109_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs3171692_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs7500738_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs7246657_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs7146643_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs578868_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs11671654_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs10248490_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs6141779_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs378112_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs17081933_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs4235404_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs3809627_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs3091338_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs2143950_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs2038255_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs6890853_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs9274623_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs140522_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs9274623_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs28383314_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs372883_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs730775_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs730775_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs9859077_meqtl/all.pdf",
		DGRAPH_PLOTS + "rs79755767_eqtlgen/all.pdf",
		DGRAPH_PLOTS + "rs79755767_meqtl/all.pdf"
